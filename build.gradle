plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id('io.github.gradle-nexus.publish-plugin') version ('1.1.0')
    id 'signing'
}

group 'org.moormanity'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}
java {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        csv.enabled true
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'java-smpte-timecode'
                description = 'A minimalist library for dealing with SMPTE timecodes'
                url = 'https://github.com/moormanm/java-smpte-timecode'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'michael.e.moorman@gmail.com'
                        name = 'Mike Moorman'
                    }
                }
                scm {
                    url = 'https://github.com/moormanm/java-smpte-timecode.git'
                    connection = 'scm:git://github.com/moormanm/java-smpte-timecode.git'
                    developerConnection = 'scm:git://github.com/moormanm/java-smpte-timecode.git'
                }
            }
        }
    }
}
nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}